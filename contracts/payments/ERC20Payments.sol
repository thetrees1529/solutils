//SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

library ERC20Payments {

    using SafeERC20 for IERC20;

    struct Payee {
        address addr;
        uint weighting;
    }

    function split(IERC20 token, uint value, Payee[] memory payees) internal {
        splitFrom(token, address(this), value, payees);
    }

    function splitFrom(IERC20 token, address from, uint value, Payee[] memory payees) internal {
        uint totalWeighting;
        for(uint i; i < payees.length; i ++) {
            totalWeighting += payees[i].weighting;
        }
        for(uint i; i < payees.length; i ++) {
            Payee memory payee = payees[i];
            uint payment = (payee.weighting * value) / totalWeighting;
            _send(token,from, payee.addr, payment);
        }
    }

    function withdraw(IERC20 token) internal {
        _send(token, address(this), msg.sender, token.balanceOf(address(this)));
    }

    function _send(IERC20 token, address from, address to, uint value) private {
        if(from == address(this)) token.safeTransfer(to,value);
        else token.safeTransferFrom(from, to, value);
    }

}